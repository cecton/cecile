pkgname=dwm-git
_pkgname=dwm
pkgver=6.0.1.g3371c07
pkgver(){
  cd $_pkgname
  #git checkout tags/6.0
  git describe --tags |sed 's/-/./g'
}
pkgrel=1

pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama')
makedepends=('git')
provides=('dwm')
conflicts=('dwm')
epoch=1
source=("$_pkgname::git+https://github.com/cecton/dwm.git")
md5sums=('SKIP')

prepare() {
  if [[ -f $SRCDEST/config.h ]]; then
    ln -sf $SRCDEST/config.h $srcdir/dwm/config.h
  fi
}

build() {
  cd $_pkgname
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  make -C $_pkgname PREFIX=/usr DESTDIR=$pkgdir install
  install -m644 -D $_pkgname/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -m644 -D $_pkgname/README $pkgdir/usr/share/doc/$pkgname/README
}

# vim:set ts=2 sw=2 et:
